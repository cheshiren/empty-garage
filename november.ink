# a-uthor: Вячеслав Добранов
VAR msg1_seen = 0
VAR msg2_seen = 0
VAR resist_to_surrender = false

* [Начать] -> Start
#* [ТЕСТ] -> Underground_crossing

=== Start
    ~ msg1_seen = Garage.read_msg1
    ~ msg2_seen = Dark_alley.read_msg2
    -> Garage

=== Garage
    # CLEAR
    #{Garage}
    Пустой гараж. <>
    {
        - Garage < 5:
            {
                - Garage == 1:
                    Ноябрь отряхивает локти от пыли и подходит к воротам. Через узкие верхние окна внутрь падает яркий свет солнечного дня.# CLASS: foo
                - Garage == 2:
                    Ноябрь хмурится, отряхивает локти от пыли и подходит к воротам. Через узкие верхние окна внутрь падает яркий свет солнечного дня.
                - Garage == 3:
                    Ноябрь хмуро оглядывается, пару секунд медлит. Подходит к воротам, задумчиво смотрит на них. Через узкие верхние окна внутрь падает яркий свет солнечного дня.
                - Garage == 4:
                    Ноябрь закрывает лицо руками и стоит так пару минут.
                - else:
                    ERROR
            }
            <></br></br>Из кармана доносится звяканье телефона — пришло новое сообщение.
            -> Garage_opts
        - Garage == 5:
            <br><br>— Это ведь не кончится, да? — Ноябрь отряхивает локти от пыли. — Что бы я ни делал, я всё равно оказываюсь в этом чёртовом гараже. Может, тебе перестать играть? Избавь меня от этого всего.
            -> Garage_argument_opts
        - Garage == 6:
            <br><br>— Аргх! Как же это больно! Надо выбираться из этого гаража, — Ноябрь отряхивает локти от пыли и подходит к двери наружу.
            Из кармана доносится звяканье телефона.
            -> Garage_opts
        - Garage >= 7:
            <br><br>— Аргх! Мать твою! — Ноябрь подскакивает к двери наружу и хватается за ручку.
            Из кармана доносится звяканье телефона.
            -> Garage_opts
    }
    = Garage_opts
        + (read_msg1){msg1_seen == read_msg1} [Прочти сообщение{read_msg1: |*}]
            {Garage == 4: -> Garage_argument("Прочти сообщение")}
            {Garage >= 7: — Да не буду я читать чёртово сообщение! -> Garage_opts}
            {Garage < 4:
                {
                    - read_msg1 == 1:
                        Он вынимает телефон из кармана.
                    - read_msg1 > 1:
                        Он вынимает телефон из кармана, долго на него смотрит, потом снимает блокировку.
                }
            }
            «Че:
            Посылка слишком болтливая! Её сейчас слышат все свиньи города! Поспеши встретиться с Гаспаром, у него есть всё необходимое, чтобы её заткнуть. И не вздумай сунуться на точку, пока она отсвечивает по всему эфиру! Наведёшь свиней — я тебя живьём закопаю.
            Гаспар встретит тебя у Монумента. Быстрее!»
            -> Garage_opts
        + (go_out_garage)[Выходи на улицу{go_out_garage: |*}]
            {Garage == 4: -> Garage_argument("Выходи на улицу")}
            Ноябрь подтягивает лямки рюкзака и выходит в боковую дверь.
            -> Near_garage
    = Garage_argument(ch)
        Ноябрь отнимает руки от лица и, глядя на гаражные ворота, произносит:
        — Нет.
        * [Какого чёрта…]
            Он проходится по помещению, осматривает стены, потолок. Ощупывает себя, долго смотрит на свои ладони. Качает головой:
            — Это просто грешно.
            ** [{ch}!]
                — Я сказал нет. Это что, какой-то эксперимент? Я вроде подопытного кролика?
                *** [Нет]
                    — А что тогда? Почему я постоянно оказываюсь в этом гараже?
                *** [Не знаю]
                    — То есть, ты не знаешь? Ты заставляешь меня постоянно умирать и не знаешь, что происходит?
        -
        * [Это игра. Компьютерная программа]
            — Что? Что, чёрт возьми? Игра? Я персонаж в какой-то идиотской игре?
            ** [Вроде того]
            — И ты мной управляешь? Прыгаешь через бочки, собираешь монетки? — он фыркает. — Боже, я, похоже, чокнулся. «Господин судья, я не виноват, мне приказывал голос в моей голове».
                *** [Я не в твоей голове]
                *** [Я не управляю тобой напрямую]
        -
        — Помолчи, мне надо подумать.
        За дверью вдруг слышится какая-то возня и через секунду она с грохотом проваливается внутрь.
        Сквозь пыль и дым в помещение стремительно проникают несколько человек в чёрной экипировке и оружием наизготовку, слышится крик: «Мордой в пол, быстро!»
        Пригнувшийся было Ноябрь распрямляется и показывает средний палец. Ближайший к нему спецназовец делает два шага и с ходу бьёт его прикладом в голову.
        Вспышка.
        * [Заново] -> Start
    = Garage_argument_opts
        * [Будут и другие игроки]
            — Боже… — Ноябрь трёт глаза. — Я узнал, что такое ад. Ладно, будь по твоему. Что мне делать?
        * [Можно пройти игру до конца]
            — А что там в конце? Принцесса в замке? Финальный босс? Чудесное освобождение? Бред, — Ноябрь трёт глаза. — Ладно, как будто у меня есть выбор. Что мне делать?
        - (a)
        * (backpack) [Проверь рюкзак]
            Он стряхивает рюкзак с плеч, аккуратно расстёгивает молнию замка и отворачивает клапан. Его лицо освещается жёлтым светом, исходящим от содержимого рюкзака. Он поджимает губы.
            ** [Что там?]
                Вздрогнув, он выходит из оцепенения, резко застёгивает рюкзак и набрасывает за спину:
                — Доведёшь меня до конца — узнаешь.
                *** [Серьёзно? Вот так?]
                    — Видишь, ты не единственный, кто здесь играет. Что дальше?{note && whois: — он вдруг спохватывается. — Я здесь слишком долго!|}
            -> Garage_argument_opts.a
        * (note)[Проверь карманы]
            Он хлопает себя по карманам штанов, куртки и, расстёгнув молнию кармана на рукаве, вынимает из него записку, разворачивает её:
            «Ноябрь, ищи по городу рисунки со свиньями — они значат, что рядом можно укрыться и перевести дух. Целую, Наима».
            — Хм, Наима? — он прячет записку назад в карман на рукаве{backpack && whois: и вдруг спохватывается. — Я здесь слишком долго!|.}
            -> Garage_argument_opts.a
        * (whois) [Кто такие {note: Че, Гаспар и Наима|Че и Гаспар}?]
            — Я знаю не больше твоего. Очевидно, они не те, кто пытается меня убить. Кто бы они ни были, без их помощи мне до финала, похоже, не добраться{backpack && note:, — он вдруг спохватывается. — Я здесь слишком долго!|.}
            -> Garage_argument_opts.a
        * {CHOICE_COUNT() == 0} -> Garage_argument_opts.b
        - (b)
        За дверью слышится какая-то возня и через секунду она с грохотом проваливается внутрь.
        Сквозь пыль и дым в помещение стремительно проникают несколько человек в чёрной экипировке и оружием наизготовку, слышится всё тот же крик: «Мордой в пол, быстро!»
        Ноябрь бросается на ближайшего спецназовца, подныривает под приклад, нацеленный ему в голову, и подножкой сваливает бойца на пол.
        Тесное пространство озаряют частые вспышки выстрелов.
        * [Заново] -> Start

=== Near_garage
    Улица, залитая солнечным светом. Жаркие блики от припаркованных с обеих сторон машин. Прогорклый запах фастфуда.
    Истошная трель велосипедного звонка совсем рядом.
    -> Near_garage_opts
    = Near_garage_opts
        + (jump_from_bike)[Отскочи в сторону{jump_from_bike: (!)|*}]
            {
                - Garage >= 6 && jump_from_bike > 1:
                    — Тебе нравится, когда я умираю, да? Нет уж, я лучше сделаю по-другому.
                    -> look_at_bike
                -
                    Ноябрь отпрыгивает в промежуток между стоящими у обочины машинами и оказывется на проезжей части. Белый фургон не успевает затормозить и на полном ходу сбивает его.
            Вспышка.
            ++ [Заново] -> Start
            }
        + (look_at_bike)[Оглянись на звонок{look_at_bike: |*}]
            {
                - Garage < 6:
                    Не двигаясь с места, Ноябрь поворачивается в сторону звонка. Велосипедист, визжа тормозами, виляет в сторону и, процарапав несколько машин у обочины, врезается в столб, падает.
                    «Ты что творишь, придурок!» — восклицает он, сев на асфальте.
                    ++ (show_him_a_finger)[Покажи ему средний палец и уходи{show_him_a_finger: |*}]
                        Не обернувшись, Ноябрь отмахивается от велосипедиста недвусмысленным жестом и, пропустив белый фургон, перебегает проезжую часть, где юркает в тёмный проулок. -> Dark_alley
                    ++ (run_across_street)[Просто беги на другую сторону улицы{run_across_street: |*}]
                        «Эй, стой, я с тобой говорю!» — раздаётся за спиной, когда Ноябрь, пропустив белый фургон, перебегает проезжую часть и юркает в тёмный проулок. -> Dark_alley
                - Garage == 6:
                    Ноябрь поворачивается в сторону звонка и делает шаг назад. Велосипедист, едва не задев его, проносится мимо и бросает за спину: «Придурок!»
                    К проезжей части. Пропустить белый фургон. На другую сторону в тёмный проулок. -> Dark_alley
                - Garage == 7:
                    Ноябрь отступает на шаг, заносит ногу и, когда велосипедист проносится мимо, с силой толкает его. Тот виляет в сторону, врезается в машину у обочины и перелетает через капот на проезжую часть. Прямо под колёса белого фургона.
                    «Сам придурок», — бормочет Ноябрь, перебегая на другую сторону и ныряя в тёмный проулок. -> Dark_alley
                - Garage > 7:
                    Ноябрь равнодушно наблюдает падение велосипедиста, затем подходит к проезжей части, пропускает белый фургон, перебегает на другую сторону и юркает в тёмный проулок. -> Dark_alley
            }

=== Dark_alley
    После яркого солнца глаза не сразу привыкают к тени. Лавируя между мусорными баками и строительными лесами, он бежит по проулку к другому выходу. Где-то воет сирена.
    Из кармана опять доносится звяканье.
    -> Dark_alley_opts
    = Dark_alley_opts
    + (read_msg2){msg2_seen == read_msg2} [Прочти сообщение{read_msg2: |*}]
        «Гаспар:
        Чувак, уходи с улиц! Если даже я вижу, где ты сейчас находишься, то свиньи и подавно тебя пасут. Давай под землю — рядом станция Северной ветки. Проедешь по ней до Кольцевой и там уже до Монумента. Я встречу».
        -> Dark_alley_opts
    + (run_further)[Беги дальше{run_further: |*}]
        Между кирпичных стен уже видно людную улицу.
        Но перед выездом с визгом останавливается чёрная машина. В проулок брызжет синий свет проблесковых маячков, затихает вой сирены.
        Человек в форме выскакивает из машины, быстро приближается, в руках — жёлтое рыло тазера. В глазах — азартный блеск. Для него это спорт.
        — Упал на землю! Живо!
        ++ (surrender)[Подчинись{surrender: (!)|*}]
            {
                - Garage >= 6 && surrender > 1:
                    {
                        - not resist_to_surrender:
                            — То есть, «подчинись»!? Я же уже это проходил, это не тот вариант!
                            — Живее! Мордой в землю!
                            — Пошёл на хер! Я не с тобой разговариваю!
                            Треск тазера. Вспышка.
                            ~ resist_to_surrender = true
                            +++ [Заново] -> Start
                        - 
                            — Это не тот вариант, — шипит Ноябрь, поднимая руки и обращаясь к полицейскому:
                            -> fight
                    }
                - 
                    — Хорошо-хорошо, офицер, — Ноябрь медленно опускается на корточки.
                    — Живее! Мордой в землю!
                    — Да иди ты. Сам ложись в эту гря…
                    Треск тазера. Вспышка.
                    +++ [Заново] -> Start
            }
        ++ (fight)[Сопротивляйся{fight: |*}]
            — Офицер, это какая-то ошибка.
            — Ты что, глухой? Я сказал на землю!
            Ноябрь опускается, делает вид, что подчиняется и с низкого старта бросается вперёд. Над головой проносится стрёкот тазерных проводов. Мимо.
            Полицейский сгибается пополам от удара в солнечное сплетение, пытается вдохнуть. Следующий удар — с размаху в челюсть. Готов.
            {
                - Garage >= 6 && not get_taser:
                    Ноябрь кивает на бесчуственного копа:
                    — Его тазер. Может пригодиться.
                    +++ (get_taser) [Подбери тазер*]
                        Он переворачивает полицейского, расжимает ему пальцы и забирает чёрный с жёлтыми полосами тазер. Отстёгивает использованный картридж с бахромой проводов, оголяя контакты, пару раз стрекочет и, удовлетворённый, прячет оружие в карман.
                        ++++ [Уходи из проулка{go_out_alley: |*}] -> Crowded_street
                - get_taser:
                    Ноябрь переворачивает копа, забирает его тазер и, отстегнув картридж, прячет в карман.
            }
            +++ (go_out_alley)[Уходи из проулка{go_out_alley: |*}] -> Crowded_street
            
=== Crowded_street
    Ноябрь выскакивает из проулка на круглую площадь, запруженную автомобилями. В центре площади возвышается колоннада с большой красной «М» над ней и единственный путь туда — подземный переход под проезжей частью.
    Однако, на стоянке рядом он замечает полицейского мотоциклиста с рацией в руках.
    -> Round_plaza_opts
    = Round_plaza_opts
    + (run_to_underground) [Беги к переходу{run_to_underground: |*}]
        Рывок к гранитной лестнице, мимо прохожих и уличных торговцев. Полицейский замечает его в толпе и бросается наперерез, на ходу вынимая дубинку.
            ++ (dont_stop)[Не останавливайся{dont_stop:(!)|*}]
                Ускорившись, Ноябрь пробегает сквозь группу заголосивших туристов и собирается перепрыгнуть через лоток продавца сувениров, когда сбоку мелькает что-то чёрное, и в следующее мгновение полицейский сбивает его с ног.
                Падение очень болезненное и травмирующее. С трудом дыша, Ноябрь переворачивается на спину, видит своё отражение в зеркальных очках и занесённую дубинку.
                Вспышка.
                +++ [Заново] -> Start
            ++ (knock_him_out)[Выруби его как и прошлого{knock_him_out:(!)|*}]
                Не останавливаясь, Ноябрь сворачивает, бежит навстречу полицейскому, ныряет под его занесённую дубинку и бьёт в корпус.
                Однако, мотоциклетная защита полностью гасит удар и реакция следует незамедлительно.
                Вспышка.
                +++ [Заново] -> Start
            ++ (taser_him) {Dark_alley.get_taser} [Выруби его шокером{taser_him: |*}]
                Не останавливаясь, Ноябрь сворачивает, бежит навстречу полицейскому, ныряет под его занесённую дубинку и прижимает стрекочущий тазер к незащищённой шее. Полицейский дёргается, падает на асфальт, но Ноябрь всё жмёт на кнопку, пока тот не теряет сознание.
                +++ (go_underground)[Спускайся в переход{go_underground: |*}] -> Underground_crossing
    + (run_across_road) [Давай через проезжую часть{run_across_road: (!)|*}]
        Он перемахивает через ограждение, отделяющее троутар от проезжей части, и бежит поперёк полос, уворачиваясь от проносящихся машин. Ему сигналят сзади, сбоку, спереди.
        Когда он уже преодолевает две трети пути клаксоны вдруг заглушает слишком громкий и слишком близкий визг тормозов.
        Вспышка.
        ++ [Заново] -> Start
        
=== Underground_crossing
    Вниз по лестнице, прочь от поднявшегося гвалта.
    Широкий и низкий коридор с рядом колонн посередине и неглубокими альковами по сторонам. К одной из колонн приклеен флаер с цветным рисунком: поросёнком в полицейской фуражке.
    Визг кроссовок по гладкому каменному полу, стенанья волынки поодаль — в одном из альковов расположился уличный музыкант. Его окружают туристы, другие прохожие спешат в обе стороны.
    — Она писала, что нужно искать рисунки с полицейскими, — Ноябрь показывает на флаер.
    -> Underground_crossing_opts
    = Underground_crossing_opts
        * (check_flyer)[Проверь флаер*]
            Он подходит ближе и изучает рисунок. Флаер ничего не рекламирует, это просто аляповатое изображение розового поросёнка в чёрной полицейской фуражке, на спине поросёнка нарисована записка: «Ударь меня».
            Ноябрь пожимает плечами и тыкает пальцем поросёнку в пятачок.
            <center>ПРОГРЕСС СОХРАНЁН</center>
            ** [Ого, это чекпоинт]
                — Что это значит?
                *** [После смерти продолжишь отсюда]
                    — То есть никакого больше гаража? Ну хоть какой-то позитив.
                    -> Underground_crossing_opts
        + (run_further){check_flyer}[Беги дальше{run_further: |*}]
            Ноябрь поправляет рюкзак и припускает к дальней лестнице. Мимо зевак, волынщика, туда, где никогда не кончается солнечный день.
            ** [ПРОДОЛЖЕНИЕ СЛЕДУЕТ]
        - <center><p class="choice" style="padding-top:1em;"><span style="font-weight:700;font-size:15pt;">ПРОДОЛЖЕНИЕ СЛЕДУЕТ ?</span></p></center>
































    
    
    
    
-> END